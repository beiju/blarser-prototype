?start: (flyout_full | ground_out_full | fielders_choice | double_play_full)

flyout_full: flyout (_N sacrifice)*
flyout: NAME " hit a flyout to " NAME "."

ground_out_full: ground_out (_N sacrifice)*
ground_out: NAME " hit a ground out to " NAME "."

double_play_full: double_play (_N score)*
double_play: NAME " hit into a double play!"

fielders_choice: runner_out (_N score)* _N batter_reaches
runner_out: NAME " out at " BASE " base."
batter_reaches: NAME " reaches on fielder's choice."

sacrifice: NAME " advances on the sacrifice." (_N use_free_refill)?
         | NAME " tags up and scores!" (_N use_free_refill)?

BASE: "first" | "second" | "third" | "fourth" | "fifth"


%import .blaseball.NAME
%import .blaseball._N
%import .blaseball.score
%import .blaseball.use_free_refill
